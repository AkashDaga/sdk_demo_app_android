apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.zendesk.rememberthedate"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 10
        versionName "1.5"
    }

    signingConfigs {
        release {
            storeFile file("../scripts/rtd.jks")
            storePassword System.getenv("RTD_STORE_PASS")
            keyAlias System.getenv("RTD_ALIAS")
            keyPassword System.getenv("RTD_KEY_PASS")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // Don't change this to false
        abortOnError true
        lintConfig file("lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

/*
 * Remove |
 *        V
 */
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def repoUsername = "$System.env.zdRepoUsername" == "null" ?
        zdRepoUsername :
        "$System.env.zdRepoUsername"

def repoPassword = "$System.env.zdRepoPassword" == "null" ?
        zdRepoPassword :
        "$System.env.zdRepoPassword"

repositories {
    maven {
        credentials {
            username repoUsername
            password repoPassword
        }

        url 'https://zdrepo.artifactoryonline.com/zdrepo/repo/'
    }
}
/*
 * Remove End
 */

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v13:22.2.1'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'

    compile group: 'com.zendesk', name: 'sdk', version: '1.4.0.1-SNAPSHOT', changing: true
    compile group: 'com.zopim.android', name: 'sdk', version: '1.0.1'
}